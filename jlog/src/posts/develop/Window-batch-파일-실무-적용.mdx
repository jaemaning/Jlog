---
title: "Window batch 파일 실무 적용! -1"
description: "window batch 파일을 실무에 적용해보자 - 간단한 폴더 구조 잡는 것부터 엑셀 파일을 읽고 파일과 매칭해 작업하는 것까지 진행했다."
date: 2024-04-04T08:23:29.620Z
tags: ["batch","window","배치파일"]
category : "Python"
---
올해 공기업에서 데이터를 다루는 아르바이트를 진행하며 느끼는 점은 단순 반복이 정말많다..

어떻게 이 일을 계속 반복해서 해오신걸까 엄청 많진 않아 가능은 하지만 매우 귀찮고 휴먼 에러 가능성++

사실 제목이 **"window batch 파일 실무 적용! -1"** 이지만 왠지 3탄인 느낌
(처음에 작업할 땐 이렇게 자동화할일이 많을줄 몰랐다)

## 오늘 내가 한일은?

- window batch 파일을 이용해 엑셀 파일을 읽고 변수로 저장
- 타겟 폴더 내(하위폴더까지 재귀적 탐색)에 엑셀 파일에 있던 데이터 변수명에 대한 파일이 존재하는지 확인
- 파일이 존재하면 output/ 폴더로 copy, 존재하지 않으면 텍스트파일에 파일 이름 체크

그렇게 복잡하지 않아보여 파이썬 코드로 작성했다면 1~2시간이면 끝났을 것 같지만 익숙하지 않은
내가 사용할 파일이 아니라서 window batch 파일로 작성하다보니 3시간정도 걸렸던것 같다.

## window batch 파일을 이용해 엑셀 파일을 읽고 변수로 저장

- 배치파일 자체에 엑셀을 읽으려면 VBScript 라는걸 이용해야하는 것 같았다. Temp 폴더 내 VBSscript 를 생성해 임시로 엑셀 데이터를 저장하는 느낌인 것 같았는데 왜 인지 Temp 폴더 내 파일 인식을 못하였고, 다른 방식을 찾았다.

- powershell Script를 이용함. test.ps1 작성
```bash
# Excel 애플리케이션 객체 생성
$excel = New-Object -ComObject Excel.Application
$excel.Visible = $False # Excel 창을 보이지 않게 설정

# workbook 열기
$workbook = $excel.Workbooks.Open((Get-Location).Path + "\test.xlsx")
$sheet = $workbook.Sheets.Item(1) # 첫 번째 시트
$range = $sheet.UsedRange
$data = $range.Value2

# Excel 종료
$excel.Quit()

# 데이터 출력
ForEach ($row in $data) {
    ForEach ($col in $row) {
        Write-Output $col
    }
}

```
- PowerShell Script 는 처음 작성해보는데 생각보다 엑셀 속 빈 Null 값 처리를 잘해주어서 놀랐다. 
> `$sheet.UsedRange` 옵션에 값이 들어있는 최소행, 최소열, 최대행, 최대열을 찾아 `$range.Value2` 옵션으로 2차원 배열로 저장시키는 원리인데 중간에 빈셀이 존재할 경우 값이 Null로 바뀌어 들어가게 된다. 그러나 데이터 출력부분에서 `Write-Output $col` 진행시 Null 값은 출력하지 않게 되어 빈 Null 값을 처리한 것처럼 보이는 것 같았다.


## 타겟 폴더의 하위폴더까지 재귀적 탐색후 저장
```bash
@echo off
SETLOCAL EnableDelayedExpansion

pause
echo .
echo .

set "inputFolder=.\input"
set "outputFolder=.\output"
set "outputFile=ListOutput.txt"

REM 파일 초기화
if exist "%outputFile%" del "%outputFile%"

REM PowerShell 스크립트 실행 및 출력값 처리
for /f "tokens=*" %%a in ('powershell -ExecutionPolicy Bypass -File ".\test.ps1"') do (
    set "fileName=%%a"
    echo Searching for: !fileName!.txt
    
    REM 하위 폴더 포함 파일 검색 및 복사
    set "fileFound=false"
    for /f "tokens=*" %%b in ('dir /s /b "%inputFolder%\!fileName!.txt" 2^>nul') do (
        set "fileFound=true"
        echo Found: %%b
        copy "%%b" "%outputFolder%\!fileName!.txt"
    )
    
    if "!fileFound!"=="false" (
        echo Not found: !fileName!.txt >> "%outputFile%"
    )

)

echo .
echo .
pause
ENDLOCAL
```
- `'powershell -ExecutionPolicy Bypass -File ".\test.ps1"'` 명령어를 통해 ps1 코드를 실행한 결과를 바로 for-do 문으로 `%%a` 변수에 받아 사용할 수 있고, 다른 cmd 기본 디렉토리 구조를 보여주는 명령어인 `dir /s /b` 를 또한 for-do 문으로 바로 변수에 담아 동작 시켜 볼 수도 있다.
> **dir /s /b** : dir 폴더 구조를 출력하는 window cmd 명령어에서 /s /b 옵션을 적용
**/s** : "Sub Directory" 의 약자로 하위 폴더까지 탐색한 결과를 보여달라는 옵션
**/b** : "Bare format" 의 약자로 dir 명령어의 출력에서 파일이나 폴더의 이름만을 간략하게 보여달라는 옵션 
(즉, 파일이나 폴더의 크기, 생성 날짜, 시간 등의 추가 정보 없이 경로와 파일 이름만 표시됨)


![](/images/46f8150d-a552-4656-ab64-e012ef06f199-image.png)

- 결과적으로 다음과 같은 폴더트리를 가지고 있더라도 `dir /s` 옵션으로 재귀적으로 input의 하위폴더를 탐색하여 결과를 for-do 문으로 작업을 수행하게 된다.

![](/images/ef1e5711-e0af-440b-9a4e-01192222dbfc-image.png)

- 작업 수행 결과는 아래처럼 엑셀 파일을 탐색후 엑셀에 있는 데이터의 파일이 존재할 경우에 output/ 폴더로의 복사를 진행하고 존재하지 않을 경우 파일명을 따로 텍스트파일로 생성하게 된다.

![](/images/281184f4-e67b-4969-950a-c7fcb795eba7-image.png)



## 정리
- window batch 파일을 이용해 excel 파일을 직접 열어 데이터를 처리하는 방식도 있지만 powershell script를 이용하여 excel 파일 reading 처리를 함.
- powershell script 로 range 를 정확히 지정하지 않아도 출력시 Null 값은 제외하고 출력해주기 때문에 따로 Null 값 처리가 필요없어 사용이 간단함.
- for-do 문에 출력과 관련된 부분이라면 모든 데이터를 넣어 처리할 수 있음.


위는 예시 코드를 가지고 작업한 결과 및 코드로 실제 제가 다니는 기업의 소스코드와는 무관합니다. 

---
### 참고자료

- 출처 : https://woshub.com/read-write-excel-files-powershell/
- 출처 : https://www.getfishtank.com/blog/useful-powershell-commands-for-excel
